// 导入 external 全局变量
apply from: "ext.gradle"

buildscript {
    println("${project.name}: buildscript")

    // rootProject.buildscript 里用的变量, 由于调用时机比 "闭包project.ext{ ... }" 早, 故不能写在那边
    project.ext.kotlin_version = "1.1.3-2"
    project.ext.android_plugin_version = "2.2.0"

    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$project.ext.android_plugin_version"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$project.ext.kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$project.ext.kotlin_version"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    def isAppModule = it.name.equals('app')
    def enableDataBinding = isAppModule || it.name.equals('databinding')

    apply plugin: isAppModule? 'com.android.application': 'com.android.library'

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'   // 拓展

    if(enableDataBinding)
        apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion rootProject.ext.compileSdkVersion as Integer
        buildToolsVersion rootProject.ext.buildToolsVersion as String

        defaultConfig {
            if(isAppModule)
                applicationId "com.fashare.mvvm_juejin"

            minSdkVersion rootProject.ext.minSdkVersion as Integer
            targetSdkVersion rootProject.ext.targetSdkVersion as Integer
            versionCode rootProject.ext.versionCode as Integer
            versionName rootProject.ext.versionName as String
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

        sourceSets {
            main.java.srcDirs += 'src/main/kotlin'
        }

        if(enableDataBinding){
            dataBinding {
                enabled true
            }
        }
    }

    dependencies {
        // kotlin
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        /******* rx ****************************************************************************/
        compile "io.reactivex.rxjava2:rxjava:$rootProject.ext.rxjavaVersion"    //1.3MB [EventBus]
        compile "io.reactivex.rxjava2:rxandroid:$rootProject.ext.rxandroidVersion" //8KB

        if(enableDataBinding){
            kapt "com.android.databinding:compiler:$rootProject.ext.android_plugin_version"

//            compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter:1.1.0'
//            compile 'me.tatarka.bindingcollectionadapter:bindingcollectionadapter-recyclerview:1.1.0'
        }
    }
}

//apply from: "https://raw.githubusercontent.com/fashare2015/gradle-screen-record/master/screen-record.gradle"
